import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21

group = "meteordevelopment"
version = "0.1.3"

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven {
        name = "enginehub"
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.13-SNAPSHOT'
    compileOnly('com.fastasyncworldedit:FastAsyncWorldEdit-Core:2.12.2') { transitive = false }
    compileOnly "net.luckperms:api:5.4"

    implementation "org.reflections:reflections:0.10.2"
    implementation "com.github.Querz:NBT:5.5"
    implementation "io.prometheus:simpleclient:0.16.0"
    implementation "io.prometheus:simpleclient_httpserver:0.16.0"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.WARN

    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

task copyJar(type: Copy) {
    from "build/libs/meteor-pvp-" + version + ".jar"
    into "run/plugins"
}
